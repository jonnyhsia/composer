apply plugin: 'com.android.library'
apply plugin: 'com.novoda.bintray-release'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 27

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        switch (requested.group){
            case 'com.android.support':
                details.useVersion '27.1.1'
                break
            case 'io.reactivex.rxjava2':
                if (requested.name == 'rxjava')
                    details.useVersion '2.1.7'
                else if (requested.name == 'rxandroid')
                    details.useVersion '2.0.1'
                break
            case 'com.android.support.constraint':
                details.useVersion '1.1.0'
                break
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // kotlin
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    api "androidx.core:core-ktx:0.3"

    implementation "com.android.support:appcompat-v7:$rootProject.support_lib_version"
    implementation "com.android.support:support-compat:$rootProject.support_lib_version"
    implementation "com.android.support:support-core-ui:$rootProject.support_lib_version"
    implementation "com.android.support:support-core-utils:$rootProject.support_lib_version"
    implementation "com.jakewharton.rxbinding2:rxbinding-kotlin:$rootProject.rxbinding_version"

    // glide
    implementation "com.github.bumptech.glide:glide:$rootProject.glide_version"
    kapt "com.github.bumptech.glide:compiler:$rootProject.glide_version"

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}

publish {
    userOrg = 'jonnyhsia'
    groupId = 'com.newe'
    artifactId = 'composer-core'
    publishVersion = '0.1.0'
    desc = 'Core utils for android dev.'
    website = 'https://github.com/jonnyhsia/composer'
}