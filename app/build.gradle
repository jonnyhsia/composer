apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 27

    defaultConfig {
        applicationId "com.jonnyhsia.composer"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName "$rootProject.versionName"

        manifestPlaceholders = [bugly_appid      : rootProject.buglyAppId,
                                bugly_app_version: rootProject.versionName]

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        switch (requested.group) {
            case 'com.android.support':
                details.useVersion '27.1.1'
                break
            case 'io.reactivex.rxjava2':
                if (requested.name == 'rxjava')
                    details.useVersion '2.1.7'
                else if (requested.name == 'rxandroid')
                    details.useVersion '2.0.1'
                break
            case 'com.android.support.constraint':
                details.useVersion '1.1.0'
                break
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':core')
    implementation project(':model')
    implementation project(':uilib')
    implementation project(':highlighttoolbar')
    implementation project(':router')

    // support
    implementation "com.android.support:recyclerview-v7:$rootProject.support_lib_version"
    implementation "com.android.support:design:$rootProject.support_lib_version"
    implementation "com.android.support:cardview-v7:$rootProject.support_lib_version"
    implementation "com.android.support:customtabs:$rootProject.support_lib_version"
    implementation 'com.android.support.constraint:constraint-layout:1.1.0'
    implementation "android.arch.lifecycle:runtime:1.1.0"
    kapt "android.arch.lifecycle:compiler:1.1.0"

    // rx
    implementation "io.reactivex.rxjava2:rxjava:$rootProject.rxjava_version"
    implementation "io.reactivex.rxjava2:rxandroid:$rootProject.rxandroid_version"
    implementation "com.jakewharton.rxbinding2:rxbinding-kotlin:$rootProject.rxbinding_version"

    // rxbus
    implementation "com.github.lsxiao.Apollo:core:1.0.1-fix"
    kapt "com.github.lsxiao.Apollo:processor:1.0.1-fix"

    // recycler view
    implementation 'me.drakeet.multitype:multitype-kotlin:3.4.4'
    implementation "com.github.rubensousa:gravitysnaphelper:1.3"
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.34'
    implementation 'com.dinuscxj:recycleritemdecoration:1.0.0'

    // glide
    implementation "com.github.bumptech.glide:glide:$rootProject.glide_version"
    kapt "com.github.bumptech.glide:compiler:$rootProject.glide_version"

    // status bar color
    implementation 'com.githang:status-bar-compat:0.7'

    // dev tools
    implementation "com.tencent.bugly:crashreport:2.6.6.1"
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'

    implementation 'com.jakewharton.threetenabp:threetenabp:1.1.0'
    implementation 'com.newe:label-view:0.2.5-alpha3'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}
